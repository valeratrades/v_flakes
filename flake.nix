{
  description = ''
# Nix parts collection

Collection of reusable Nix components.
See individual component descriptions in their respective directories.'';

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
  inputs.flake-utils.url = "github:numtide/flake-utils";

  outputs = { self, nixpkgs, flake-utils }:
    let
      pname = "v-utils";

      # Version constants for bundled packages - update these when bumping
      # Use partial semver (major.minor) - patch versions auto-resolve via cargo-binstall
      traceyVersion = "1.3";
      codestyleVersion = "0.2";

      parts = {
        files = (import ./files).description;
        github = (import ./github { inherit nixpkgs; }).description;
        rs = (import ./rs { inherit nixpkgs; }).description;
        py = (import ./py { inherit nixpkgs; }).description;
        tex = (import ./tex { inherit nixpkgs; }).description;
      };
    in
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = import nixpkgs { inherit system; };
        utils = import ./utils;
        files = import ./files;

        #TODO: pass `rs` once this repo has a rust-overlay input
        github = (import ./github) {
          inherit pkgs pname;
          labels.extra = [];
        };

        # README generation
        readme = (import ./readme_fw) {
          inherit pkgs pname;
          rootDir = ./.;
          lastSupportedVersion = null;
          defaults = true;
          badges = [ "ci" ];
        };
      in
      {
        devShells.default = pkgs.mkShell {
          packages = with pkgs; [ curl ] ++ github.enabledPackages;
          shellHook = ''
            _bump_script="./__scripts/bump_crate.rs"
            ${utils.checkCrateVersion { name = "tracey"; currentVersion = traceyVersion; bumpScript = "$_bump_script"; }}
            ${utils.checkCrateVersion { name = "codestyle"; currentVersion = codestyleVersion; bumpScript = "$_bump_script"; }}
            cp -f ${(files.gitignore { inherit pkgs; langs = [];})} ./.gitignore
            ${readme.shellHook}
            ${github.labelSyncHook}
          '';
        };
      }
    ) // {
      description = ''
## Files
${parts.files}

## GitHub
${parts.github}

## Rust
${parts.rs}

## Python
${parts.py}

## LaTeX
${parts.tex}

## Readme Framework
Generates README.md from .readme_assets/ directory structure.
'';

      files = import ./files;
      github = import ./github;
      rs = import ./rs;
      py = import ./py;
      tex = import ./tex;
      readme-fw = import ./readme_fw;
      utils = import ./utils;

      # Backward compatibility aliases
      hooks = {
        description = "DEPRECATED: Use github module instead";
        appendCustom = ./github/append_custom.rs;
        treefmt = import ./files/treefmt.nix;
        preCommit = import ./github/pre_commit.nix;
      };
      workflows = import ./github/workflows/nix-parts;
      ci = import ./github;
    };
}
